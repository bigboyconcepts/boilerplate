apply plugin: 'com.android.application'


// add the repositories this project is going to use to depList
String RETROFIT = "retrofit"
String ORM_LITE = "ormlite"
def depList = []

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "boulderstudios.com.boilerplate"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java {
                if (!depList.contains(RETROFIT)) {
                    exclude '**/RetrofitApiManager.java'
                }
                if (!depList.contains(ORM_LITE)) {
                    exclude '**/DatabaseHelper.java'
                    exclude '**/OrmlitePersistence.java'
                }
            }
        }
    }
}

ext {
    ext.supportLibVersion = '26.1.0'
}

dependencies {
    if (depList.contains(RETROFIT)) {
        implementation 'com.squareup.retrofit2:retrofit:2.3.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    }
    if (depList.contains(ORM_LITE)) {
        implementation 'com.j256.ormlite:ormlite-android:5.0'
    }
    if (USE_CRASHLYTICS) {
        compile('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
            transitive = true
        }
    }
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:' + supportLibVersion
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
